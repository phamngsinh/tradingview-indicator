// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© jmosullivan

// @version=5
// Created By jmosullivan
// Volume Grid

indicator(title='Volume Grid', overlay=true, scale=scale.none)

import jmosullivan/PrivateTable/5

var COLOR_MODERATE = color.rgb(40, 98, 255)
var COLOR_EXTREME = color.rgb(232, 24, 170)
var COLOR_TRANSPARENT = color.rgb(255, 255, 255, 100)

// Configuration
grp = "Layout"
topOffset = input.int(group=grp, defval=0, title="Top Offset", options=[0, 1, 2, 3])
showTitle = input.bool(group=grp, defval=true, title="Show Title")
position = input.string(group=grp, defval=position.bottom_right, title="Position", options=[position.top_right, position.bottom_right, position.bottom_left])
textSize = input.string(group=grp, defval=size.small, title="Text Size", options=[size.tiny, size.small, size.normal, size.large])
colorExtreme = input.color(group=grp, defval=COLOR_EXTREME, title="Extreme Color")
colorModerate = input.color(group=grp, defval=COLOR_MODERATE, title="Moderate Color")
length = input.int(group=grp, defval=30, title="Vol MA Length")
transpose = input.bool(group=grp, defval=true, title="Vertical Table")

// Functions

formatNumber(num) =>
    var n = num
    var ret = str.format("{0,number,#}", n)
    if (num > 999)
        n := num / 1000
        ret := str.format("{0,number,#.#}", n) + "k"
    if (num > 999999)
        n := num / 1000000
        ret := str.format("{0,number,#.#}", n) + "M"
    ret

addRow(dataArray, bgColorArray, label, vol, volMovingAverage) =>
    rvol = vol/volMovingAverage
    isExtreme = rvol >= 1
    // Add the label
    array.push(dataArray, label)
    array.push(bgColorArray, colorModerate)
    // Add the average vol
    array.push(dataArray, formatNumber(volMovingAverage))
    array.push(bgColorArray, colorModerate)
    // Add the current vol
    array.push(dataArray, formatNumber(vol))
    array.push(bgColorArray, isExtreme ? colorExtreme : colorModerate)
    // Add the rvol
    array.push(dataArray, str.format("{0,number,#.#}", rvol))
    array.push(bgColorArray, isExtreme ? colorExtreme : colorModerate)

// Logic

chartVolumeMa = ta.sma(volume, length)
dailyVolume = request.security(syminfo.tickerid, "D", volume)
dailyVolumeMa = request.security(syminfo.tickerid, "D", ta.sma(volume, length))

if barstate.islast
    titleArray = array.from("", "AVG", "CUR", "RVOL")
    titleBgColorArray = array.from(COLOR_TRANSPARENT, colorModerate, colorModerate, colorModerate)
    chartArray = array.new_string()
    chartBgColorArray = array.new_color()
    dailyArray = array.new_string()
    dailyBgColorArray = array.new_color()

    addRow(chartArray, chartBgColorArray, "Chart", volume, chartVolumeMa)
    addRow(dailyArray, dailyBgColorArray, "1D", dailyVolume, dailyVolumeMa)

    dataMatrix = matrix.new<string>()
    matrix.add_row(dataMatrix, 0, titleArray)
    matrix.add_row(dataMatrix, 1, chartArray)
    matrix.add_row(dataMatrix, 2, dailyArray)
    bgMatrix = matrix.new<color>()
    matrix.add_row(bgMatrix, 0, titleBgColorArray)
    matrix.add_row(bgMatrix, 1, chartBgColorArray)
    matrix.add_row(bgMatrix, 2, dailyBgColorArray)

    PrivateTable.fromMatrix(dataMatrix, bgMatrix, position, textSize=textSize)
