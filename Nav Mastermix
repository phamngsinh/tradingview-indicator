// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© jmosullivan

//@version=5
indicator("Nav Mastermix", shorttitle="Nav MMX", overlay=true)

// 1. Yellow inside bars
barcolor((high <= high[1] and low >= low[1]) ? color.yellow : na)

// 2. Price Pivots
ph = ta.pivothigh(3, 3)
pl = ta.pivotlow(3, 3)

drawLabel(_offset, _pivot, _style) =>
    if not na(_pivot)
        label.new(bar_index[_offset], _pivot, str.tostring(_pivot, format.mintick), style=_style, color=color.rgb(255,255,255,100), textcolor=#131722)

drawLabel(3, ph, label.style_label_down)
drawLabel(3, pl, label.style_label_up)

// 3. VWAP
vwap = ta.vwap(hlc3)
plot(vwap, color=color.purple, style=plot.style_line, linewidth=3, title="VWAP")

// 4. EMAs
ema9 = ta.ema(close, 9)
ema21 = ta.ema(close, 21)
plot(ema9, color=color.blue, style=plot.style_line, title="ema 9")
plot(ema21, color=color.red, style=plot.style_line, title="ema 21")

// 5. Highlight the open
int first15 = na
int first30 = na
if (timeframe.isminutes or timeframe.isseconds)
    first15 := time(timeframe.period, "0930-0945:23456")
    first30 := time(timeframe.period, "0945-1000:23456")

bgcolor(first15 ? color.rgb(178, 255, 160, 50) : na)
bgcolor(first30 ? color.rgb(202, 255, 190, 60) : na)
